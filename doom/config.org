#+TITLE: Doom config
#+AUTHOR: Lumi Denoir
#+DESCRIPTION: Literate config file for doom Emacs
#+PROPERTY: header-args :tangle config.el
#+STARTUP: overview

*Place your private configuration here! Remember, you do not need to run =doom sync= after modifying this file! Just do =SPC h r r= or ~M-x doom/reload~.*

* Identity config
Some functionality uses this to identify you, e.g. GPG configuration, email clients, file templates and snippets. It is optional.

#+begin_src emacs-lisp :tangle yes
;;; ~/.config/doom/config.el
(setq user-full-name "Lumi Denoir"
      user-mail-address "lumidenoir@gmail.com")
#+end_src

* Theme and Font
Doom exposes five (optional) variables for controlling fonts in Doom:
- =doom-font= the primary font to use
- =doom-variable-pitch-font= a non-mono-space font (where applicable)
- =doom-big-font= used for `doom-big-font-mode'; use this for presentations or streaming.
- =doom-symbol-font= for symbols
- =doom-serif-font= for the `fixed-pitch-serif' face

#+begin_src emacs-lisp :tangle yes
(setq doom-font (font-spec :family "Iosevka" :size 15 :weight 'regular :slant 'normal :width 'expanded :spacing 90)
      doom-big-font-mode (font-spec :family "Iosevka" :size 25)
      doom-variable-pitch-font (font-spec :family "Iosevka Etoile" :size 16)
      doom-emoji-font (font-spec :family "Noto Color Emoji")
      doom-symbol-font (font-spec :family "Iosevka")
      doom-serif-font (font-spec :family "IBM Plex Mono" :size 17 :weight 'light)
      doom-theme 'doom-one
      display-line-numbers-type nil)
#+end_src

1. See ~C-h v doom-font~ for documentation and more examples of what they accept. If you or Emacs can't find your font, use ~M-x describe-font~ to look them up, ~M-x eval-region~ to execute elisp code, and ~M-x doom/reload-font~ to refresh your font settings. If Emacs still can't find your font, it likely wasn't installed correctly. Font issues are rarely Doom issues!
2. There are two ways to load a theme. Both assume the theme is installed and available. You can either set =doom-theme= or manually load a theme with the ~load-theme~ function.
3. This determines the style of line numbers in effect. If set to =nil=, line numbers are disabled. For relative line numbers, set this to =relative=.

#+begin_src emacs-lisp :tangle yes
;;HACK to replace evil keyword in which-key popup
(setq which-key-idle-delay 0.5)
(setq which-key-allow-multiple-replacements t)
(after! which-key
  (pushnew!
   which-key-replacement-alist
   '(("" . "\\`+?evil[-:]?\\(?:a-\\)?\\(.*\\)") . (nil . "‚óÇ\\1"))
   '(("\\`g s" . "\\`evilem--?motion-\\(.*\\)") . (nil . "‚óÉ\\1"))
   ))
#+end_src

* Org Configuration
** Setting some org necessities
If you use =org= and don't want your org files in the default location below, change =org-directory=. It must be set before org loads!

#+begin_src emacs-lisp :tangle yes
;; Setting the directory for Org files and other related paths
(setq org-directory (file-truename "~/org/")
      org-hugo-base-dir (file-truename "~/Public/")
      org-noter-notes-search-path '("~/org/") ;; Path for org-noter notes
      org-hide-emphasis-markers t  ;; Hide markers like *, /, =, etc.
      org-log-done 'time) ;; Log the time when a TODO is marked as DONE
(add-hook 'org-mode-hook #'+org-pretty-mode)
;; Enable org-fragtog-mode for automatic LaTeX fragment toggling
(add-hook 'org-mode-hook 'org-fragtog-mode)
(setq org-startup-with-latex-preview t) ;; Start with LaTeX preview enabled
(setq org-cite-csl-styles-dir "/home/lumi/Zotero/styles") ;; Path to CSL styles for citations

(setq reftex-default-bibliography "/home/lumi/org/zotero.bib"
      org-agenda-files '("/home/lumi/org/todo.org" "/home/lumi/org/todoist.org")
      org-fold-catch-invisible-edits 'smart)
(after! org
  (setq org-adapt-indentation t)
  ;; Set some faces for various org elements
  (custom-set-faces!
    ;; Customize org-quote
    `((org-quote)
      :foreground ,(doom-color 'blue) :extend t :italic t)
    ;; Customize org-verse
    `((org-verse)
      :foreground ,(doom-color 'yellow)  :extend t :italic t))
  ;; Change how LaTeX and image previews are shown
  (setq org-highlight-latex-and-related '(native entities script)))
(setq org-src-window-setup 'current-window)

(setq org-src-fontify-natively t
      org-src-tab-acts-natively t
      org-edit-src-content-indentation 0)

(defun locally-defer-font-lock ()
  "Set jit-lock defer and stealth, when buffer is over a certain size."
  (when (> (buffer-size) 50000)
    (setq-local jit-lock-defer-time 0.05
                jit-lock-stealth-time 1)))
(add-hook 'org-mode-hook #'locally-defer-font-lock)

(setq org-agenda-deadline-faces
      '((1.001 . error)
        (1.0 . org-warning)
        (0.5 . org-upcoming-deadline)
        (0.0 . org-upcoming-distant-deadline)))


(after! org
;;Resize Org headings
(dolist (face '((org-level-1 . 1.4)
                (org-level-2 . 1.3)
                (org-level-3 . 1.2)
                (org-level-4 . 1.1)
                (org-level-5 . 1.1)
                (org-level-6 . 1.1)
                (org-level-7 . 1.1)
                (org-level-8 . 1.1)))
  (set-face-attribute (car face) nil :font "Iosevka Etoile" :weight 'bold :height (cdr face)))

;; Make the document title a bit bigger
  (set-face-attribute 'org-document-title nil :font "Alegreya" :weight 'bold :height 1.8))
(setq org-ellipsis " ‚ñæ ")
#+end_src

** Export settings

#+begin_src emacs-lisp :tangle yes
(setq org-export-headline-levels 5) ; I like nesting

(defvar org-reference-contraction-max-words 3
  "Maximum number of words in a reference reference.")
(defvar org-reference-contraction-max-length 35
  "Maximum length of resulting reference reference, including joining characters.")
(defvar org-reference-contraction-stripped-words
  '("the" "on" "in" "off" "a" "for" "by" "of" "and" "is" "to" "as")
  "Superfluous words to be removed from a reference.")
(defvar org-reference-contraction-joining-char "-"
  "Character used to join words in the reference reference.")

(defun org-reference-contraction-truncate-words (words)
  "Using `org-reference-contraction-max-length' as the total character 'budget' for the WORDS
and truncate individual words to conform to this budget.

To arrive at a budget that accounts for words undershooting their requisite average length,
the number of characters in the budget freed by short words is distributed among the words
exceeding the average length.  This adjusts the per-word budget to be the maximum feasable for
this particular situation, rather than the universal maximum average.

This budget-adjusted per-word maximum length is given by the mathematical expression below:

max length = \\floor{ \\frac{total length - chars for seperators - \\sum_{word \\leq average length} length(word) }{num(words) > average length} }"
  ;; trucate each word to a max word length determined by
  ;;
  (let* ((total-length-budget (- org-reference-contraction-max-length  ; how many non-separator chars we can use
                                 (1- (length words))))
         (word-length-budget (/ total-length-budget                      ; max length of each word to keep within budget
                                org-reference-contraction-max-words))
         (num-overlong (-count (lambda (word)                            ; how many words exceed that budget
                                 (> (length word) word-length-budget))
                               words))
         (total-short-length (-sum (mapcar (lambda (word)                ; total length of words under that budget
                                             (if (<= (length word) word-length-budget)
                                                 (length word) 0))
                                           words)))
         (max-length (/ (- total-length-budget total-short-length)       ; max(max-length) that we can have to fit within the budget
                        num-overlong)))
    (mapcar (lambda (word)
              (if (<= (length word) max-length)
                  word
                (substring word 0 max-length)))
            words)))

(defun org-reference-contraction (reference-string)
  "Give a contracted form of REFERENCE-STRING that is only contains alphanumeric characters.
Strips 'joining' words present in `org-reference-contraction-stripped-words',
and then limits the result to the first `org-reference-contraction-max-words' words.
If the total length is > `org-reference-contraction-max-length' then individual words are
truncated to fit within the limit using `org-reference-contraction-truncate-words'."
  (let ((reference-words
         (cl-remove-if-not
          (lambda (word)
            (not (member word org-reference-contraction-stripped-words)))
          (let ((str reference-string))
            (setq str (downcase str))
            (setq str (replace-regexp-in-string "\\[\\[[^]]+\\]\\[\\([^]]+\\)\\]\\]" "\\1" str)) ; get description from org-link
            (setq str (replace-regexp-in-string "[-/ ]+" " " str)) ; replace seperator-type chars with space
            (setq str (puny-encode-string str))
            (setq str (replace-regexp-in-string "^xn--\\(.*?\\) ?-?\\([a-z0-9]+\\)$" "\\2 \\1" str)) ; rearrange punycode
            (setq str (replace-regexp-in-string "[^A-Za-z0-9 ]" "" str)) ; strip chars which need %-encoding in a uri
            (split-string str " +")))))
    (when (> (length reference-words)
             org-reference-contraction-max-words)
      (setq reference-words
            (cl-subseq reference-words 0 org-reference-contraction-max-words)))

    (when (> (apply #'+ (1- (length reference-words))
                    (mapcar #'length reference-words))
             org-reference-contraction-max-length)
      (setq reference-words (org-reference-contraction-truncate-words reference-words)))

    (string-join reference-words org-reference-contraction-joining-char)))

#+end_src

** Deft
Deft is a mode for quickly browsing, filtering, and editing notes. The following configuration ensures Deft handles Org-mode files properly.

#+begin_src emacs-lisp :tangle yes
;; Set Deft to use the first non-empty line as the title, and specify the directory
(setq deft-use-filename-as-title nil
      deft-directory "~/org/")

;; Customize Deft's summary parsing to ignore org labels and properties
(setq deft-strip-summary-regexp
      (concat "\\("
              "[\n\t]" ;; blank
              "\\|^#\\+[[:alpha:]_]+:.*$" ;; org-mode metadata
              "\\|^:PROPERTIES:\n\\(.+\n\\)+:END:\n" ;; roam metadata
              "\\)"))

;; Function to parse the title in Deft, looking for #+TITLE: in the contents
(defun cm/deft-parse-title (file contents)
  (let ((begin (string-match "^#\\+[tT][iI][tT][lL][eE]: .*$" contents)))
    (if begin
        (string-trim (substring contents begin (match-end 0)) "#\\+[tT][iI][tT][lL][eE]: *" "[\n\t ]+")
      (deft-base-filename file))))

(advice-add 'deft-parse-title :override #'cm/deft-parse-title)
#+end_src

** Custom todo
#+begin_src emacs-lisp :tangle yes
(setq org-todo-keywords
      '((sequence "TODO(t)" "PROJ(p)" "EVNT(e)" "STRT(s)" "WAIT(w)" "HOLD(h)" "REVI(r)" "IDEA(i)" "|" "DONE(d)" "KILL(k)")
        (sequence "[ ](T)" "[-](S)" "[?](W)" "|" "[X](D)")
        (sequence "|" "OKAY(o)" "YES(y)" "NO(n)")))

(setq org-todo-keyword-faces
      '(("[-]" . +org-todo-active)
        ("STRT" . +org-todo-active)
        ("[?]" . +org-todo-onhold)
        ("WAIT" . +org-todo-onhold)
        ("REVI" . +org-todo-onhold)
        ("HOLD" . +org-todo-onhold)
        ("EVNT" . +org-todo-project)
        ("PROJ" . +org-todo-project)
        ("NO" . +org-todo-cancel)
        ("KILL" . +org-todo-cancel)))
#+end_src

** Org-roam v2
Org-roam is a tool for managing a personal knowledge base with Org-mode. Here is the setup for Org-roam version 2.

#+begin_src emacs-lisp :tangle yes
;; Set the directory for Org-roam files
(setq org-roam-directory (file-truename "~/org/"))

;; Define capture templates for Org-roam
(setq org-roam-capture-templates
    '(("d" "default" plain "%?" :target
       (file+head "%<%Y%m%d%H%M%S>-${slug}.org" "#+TITLE:${title}\n#+filetags: :incomplete:\n#+DATE: %U\n#+EXPORT_FILE_NAME: ${slug}\n")
       :unnarrowed t)))

;; Configure timestamp format for Org-roam
(setq time-stamp-active t
      time-stamp-start "#\\+DATE:[ \t]*"
      time-stamp-end "$"
      time-stamp-format "\[%Y-%02m-%02d %3a %02H:%02M\]")
(add-hook 'before-save-hook 'time-stamp nil)

;; Display Org-roam buffer in a side window
(add-to-list 'display-buffer-alist
             '("\\*org-roam\\*"
               (display-buffer-in-side-window)
               (side . right)
               (slot . 0)
               (window-width . 0.33)
               (window-parameters . ((no-other-window . t)
                                     (no-delete-other-windows . t)))))

;; Define sections to display in Org-roam mode
(setq org-roam-mode-sections
      '((org-roam-backlinks-section :unique t)
        org-roam-reflinks-section))

(after! (org-roam nerd-icons-corfu)
  (add-to-list
   'nerd-icons-corfu-mapping
   '(org-roam :style "cod" :icon "symbol_interface" :face font-lock-type-face)))

(defadvice! doom-modeline--buffer-file-name-roam-aware-a (orig-fun)
  :around #'doom-modeline-buffer-file-name ; takes no args
  (if (string-match-p (regexp-quote org-roam-directory) (or buffer-file-name ""))
      (replace-regexp-in-string
       "\\(?:^\\|.*/\\)\\([0-9]\\{4\\}\\)\\([0-9]\\{2\\}\\)\\([0-9]\\{2\\}\\)[0-9]*-"
       "(\\1-\\2-\\3) "
       (subst-char-in-string ?_ ?  buffer-file-name))
    (funcall orig-fun)))
#+end_src

** Org-roam-ui
Org-roam-ui provides a graphical interface for visualizing the network of your Org-roam notes.

#+begin_src emacs-lisp :tangle yes
(use-package! websocket
  :after org-roam)

(use-package! org-roam-ui
  :after org-roam
  :config
  ;; Sync theme, follow the node in the graph, update on save, and open on start
  (setq org-roam-ui-sync-theme t
        org-roam-ui-follow t
        org-roam-ui-update-on-save t
        org-roam-ui-open-on-start t))
#+end_src

** Citar
Citar is a citation management package. The following setup integrates it with Org-mode and Org-roam.

#+begin_src emacs-lisp :tangle no
(use-package! citar
  :after oc
  :custom
  (org-cite-insert-processor 'citar)
  (org-cite-follow-processor 'citar)
  (org-cite-activate-processor 'citar)
  (citar-bibliography '("~/org/zotero.bib"))
  (citar-org-roam-note-title-template "${author} - ${title}\n"))

;; Additional paths for Citar
(setq! citar-bibliography '("/home/lumi/org/zotero.bib"))
(setq! citar-library-paths '("~/org/assets/books/")
       citar-notes-paths '("~/org/"))

(use-package! citar-org-roam
  :after (citar org-roam)
  :config (citar-org-roam-mode))

(after! citar
  ;; Define advise
  (defun hp/citar-capf-add-kind-property (orig-fun &rest args)
    "Advice around `org-roam-complete-link-at-point' to add :company-kind property."
    (let ((result (apply orig-fun args)))
      (append result '(:company-kind (lambda (_) 'reference)))))
  ;; Wraps around the relevant functions
  (advice-add 'citar-capf :around #'hp/citar-capf-add-kind-property))
(after! (org-roam kind-icon)
  (add-to-list
   'kind-icon-mapping
   `(org-roam ,(nerd-icons-codicon "nf-cod-symbol_interface") :face font-lock-type-face)))
#+end_src

** Todoist
Configuration for integrating Todoist with Emacs.

#+begin_src emacs-lisp :tangle no
(setq todoist-token "your-api-here"
      todoist-backing-buffer "~/org/todoist.org"
      todoist-show-all nil) ;; Only show tasks that are due today
#+end_src

** Noter
Settings for org-noter, a tool for annotating PDFs and other documents with Org-mode.

#+begin_src emacs-lisp :tangle no
(setq org-noter-always-create-frame nil
      org-noter-kill-frame-at-session-end nil)
#+end_src

** Org-habit
#+begin_src emacs-lisp :tangle no
(use-package! org-habit
  :custom
  (org-habit-graph-column 1)
  (org-habit-preceding-days 7)
  (org-habit-following-days 3)
  (org-habit-show-habits-only-for-today nil))
#+end_src

** Org-heatmap
#+begin_src emacs-lisp :tangle no
(use-package! org-heatmap
  :after (org)
  :custom
  (org-heatmap-db-location "/home/lumi/org/org-heatmap.db")
  :config
  (org-heatmap-mode))
#+end_src

** Super-agenda
Super-agenda groups and filters items in Org-agenda views for better organization.

#+begin_src emacs-lisp :tangle yes
(defun date-three-days-later ()
  "Return the date three days from today in the format YYYY-MM-DD."
  (let* ((today (current-time))                         ;; Get the current time
         (three-days-later (time-add today (* 3 24 60 60))) ;; Add three days to current time
         (date-string (format-time-string "%Y-%m-%d" three-days-later))) ;; Format the new date
    date-string))

;; Calculate the date three days later and store it in a variable
(let ((date-three-days-later (date-three-days-later)))
  (setq org-agenda-custom-commands
        `(("c" "Super view"
           ((alltodo "" ((org-agenda-overriding-header "")
                         (org-super-agenda-groups
                          '((:name "Actionable Today"
                             :deadline today
                             :scheduled today
                             :face (:foreground "green")
                             :order 1)
                            (:name "Overdue"
                             :deadline past
                             :face (:foreground "red")
                             :order 2)
                            (:name "Deadline soon"
                                   :face (:foreground "orange")
                                   :deadline (before ,date-three-days-later)
                                   :order 3)
                            (:habit t)
                            (:name "Scheduled for Future"
                             :scheduled future
                             :face (:foreground "blue")
                             :order 4)
                            (:name "In Progress"
                             :todo ("STRT" "WAIT" "HOLD")
                             :and (:scheduled past :deadline future)
                             :order 5)
                            (:name "Not yet started"
                             :todo ("TODO" "PROJ" "IDEA")
                             :scheduled nil
                             :deadline nil
                             :order 6))))))))))

(use-package! org-super-agenda
  :after org-agenda
  :config
  (org-super-agenda-mode))
#+end_src

** org-modern
#+begin_src emacs-lisp :tangle yes
(use-package! org-modern
  :hook (org-mode . org-modern-mode)
  :config
  (setq
   ;; Edit settings
   org-fold-catch-invisible-edits 'show-and-error
   org-special-ctrl-a/e t
   org-insert-heading-respect-content t
   ;; Appearance
   org-modern-radio-target    '("‚ù∞" t "‚ù±")
   org-modern-internal-target '("‚Ü™ " t "")
   org-modern-todo nil
   org-modern-tag t
   org-modern-star 'replace
   org-modern-block-name nil
   org-modern-timestamp nil
   org-modern-statistics nil
   org-modern-table nil
   org-modern-progress 12
   org-modern-priority nil
   org-modern-horizontal-rule "‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ"
   org-modern-keyword nil
   org-agenda-tags-column 0
   org-modern-list '((43 . "‚Ä¢")
                     (45 . "‚Äì")
                     (42 . "‚Ü™")))
  (custom-set-faces!
    ;; `((org-modern-tag)
    ;;   :background ,(doom-blend (doom-color 'blue) (doom-color 'bg) 0.1)
    ;;   :foreground ,(doom-color 'grey))
    `((org-modern-radio-target org-modern-internal-target)
      :inherit 'default :foreground ,(doom-color 'blue)))
  )
#+end_src

#+begin_src emacs-lisp :tangle yes
(use-package! org-appear
  :hook (org-mode . org-appear-mode)
  :config
  (setq org-appear-autoemphasis t
        org-appear-autosubmarkers t
        org-appear-autolinks nil)
  ;; for proper first-time setup, `org-appear--set-elements'
  ;; needs to be run after other hooks have acted.
  (run-at-time nil nil #'org-appear--set-elements))
#+end_src
** svg-tag

#+begin_src emacs-lisp :tangle yes
(use-package! svg-tag-mode
  :config
  (defconst date-re "[0-9]\\{4\\}-[0-9]\\{2\\}-[0-9]\\{2\\}")
  (defconst time-re "[0-9]\\{2\\}:[0-9]\\{2\\}")
  (defconst day-re "[A-Za-z]\\{3\\}")
  (defconst day-time-re (format "\\(%s\\)? ?\\(%s\\)?" day-re time-re))

  (defun svg-progress-percent (value)
    (save-match-data
    (svg-image (svg-lib-concat
                (svg-lib-progress-bar
                 (/ (string-to-number value) 100.0) nil
                 :height 0.8 :foreground (doom-color 'fg) :background (doom-color 'bg)
                 :margin 0 :stroke 2 :radius 3 :padding 2 :width 11)
                (svg-lib-tag (concat value "%") nil
                             :height 0.8 :foreground (doom-color 'fg) :background (doom-color 'bg)
                             :stroke 0 :margin 0)) :ascent 'center)))

(defun svg-progress-count (value)
  (save-match-data
    (let* ((seq (split-string value "/"))
           (count (if (stringp (car seq))
                      (float (string-to-number (car seq)))
                    0))
           (total (if (stringp (cadr seq))
                      (float (string-to-number (cadr seq)))
                    1000)))
      (svg-image (svg-lib-concat
                  (svg-lib-progress-bar (/ count total) nil
                                        :foreground (doom-color 'fg)
                                        :background (doom-color 'bg) :height 0.8
                                        :margin 0 :stroke 2 :radius 3 :padding 2 :width 11)
                  (svg-lib-tag value nil
                               :foreground (doom-color 'fg)
                               :background (doom-color 'bg)
                               :stroke 0 :margin 0 :height 0.8)) :ascent 'center))))

  (set-face-attribute 'svg-tag-default-face nil :family "Cartograph Sans CF")
  (setq svg-tag-tags
        `(;; Task priority e.g. [#A], [#B], or [#C]
          ("\\[#A\\]" . ((lambda (tag) (svg-tag-make tag :face 'error :inverse t :height .9
                                                     :beg 2 :end -1 :margin 0 :radius 10))))
          ("\\[#B\\]" . ((lambda (tag) (svg-tag-make tag :face 'warning :inverse t :height .9
                                                     :beg 2 :end -1 :margin 0 :radius 10))))
          ("\\[#C\\]" . ((lambda (tag) (svg-tag-make tag :face 'org-todo :inverse t :height .9
                                                     :beg 2 :end -1 :margin 0 :radius 10))))
          ;; ("\\(:#[A-Za-z0-9]+\\)" . ((lambda (tag)
          ;;                            (svg-tag-make tag :beg 2 :inverse t :margin 1 :face (doom-color 'blue) ))))
          ;; ("\\(:#[A-Za-z0-9]+:\\)$" . ((lambda (tag)
          ;;                              (svg-tag-make tag :beg 2 :end -1 :inverse t :margin 1 :face (doom-color 'blue)))))


        ;; Active date (with or without day name, with or without time)
        ;; (,(format "\\(<%s>\\)" date-re) .
        ;;  ((lambda (tag)
        ;;     (svg-tag-make tag :beg 1 :end -1 :margin 0))))
        ;; (,(format "\\(<%s \\)%s>" date-re day-time-re) .
        ;;  ((lambda (tag)
        ;;     (svg-tag-make tag :beg 1 :inverse nil :crop-right t :margin 0))))
        ;; (,(format "<%s \\(%s>\\)" date-re day-time-re) .
        ;;  ((lambda (tag)
        ;;     (svg-tag-make tag :end -1 :inverse t :crop-left t :margin 0))))

        ;; ;; Inactive date  (with or without day name, with or without time)
        ;;  (,(format "\\(\\[%s\\]\\)" date-re) .
        ;;   ((lambda (tag)
        ;;      (svg-tag-make tag :beg 1 :end -1 :margin 0 :face 'org-date))))
        ;;  (,(format "\\(\\[%s \\)%s\\]" date-re day-time-re) .
        ;;   ((lambda (tag)
        ;;      (svg-tag-make tag :beg 1 :inverse nil :crop-right t :margin 0 :face 'org-date))))
        ;;  (,(format "\\[%s \\(%s\\]\\)" date-re day-time-re) .
        ;;   ((lambda (tag)
        ;;      (svg-tag-make tag :end -1 :inverse t :crop-left t :margin 0 :face 'org-date))))

          ;; Keywords
          ("TODO" . ((lambda (tag) (svg-tag-make tag :inverse t :height .85 :face 'org-todo))))
          ("HOLD" . ((lambda (tag) (svg-tag-make tag :inverse t :height .85 :face '+org-todo-onhold))))
          ("DONE" . ((lambda (tag) (svg-tag-make tag :height .85 :face 'org-todo))))
          ("KILL" . ((lambda (tag) (svg-tag-make tag :inverse t :height .85 :face '+org-todo-cancel))))
          ("STRT\\|WAIT" . ((lambda (tag) (svg-tag-make tag :inverse t :height .85 :face '+org-todo-active))))
          ("EVNT\\|PROJ\\|IDEA" .
           ((lambda (tag) (svg-tag-make tag :inverse t :height .85 :face '+org-todo-project))))
          ("REVI" . ((lambda (tag) (svg-tag-make tag :inverse t :height .85 :face '+org-todo-onhold))))))

  :hook (org-mode . svg-tag-mode))
#+end_src

#+begin_src emacs-lisp :tangle no
;; HACK for showing svg-tag overlay in agenda mode
  (defun org-agenda-show-svg ()
    (let* ((case-fold-search nil)
           (keywords (mapcar #'svg-tag--build-keywords svg-tag--active-tags))
           (keyword (car keywords)))
      (while keyword
        (save-excursion
          (while (re-search-forward (nth 0 keyword) nil t)
            (overlay-put (make-overlay
                          (match-beginning 0) (match-end 0))
                         'display  (nth 3 (eval (nth 2 keyword)))) ))
        (pop keywords)
        (setq keyword (car keywords)))))
  (add-hook 'org-agenda-finalize-hook #'org-agenda-show-svg)
#+end_src

** zen mode
#+begin_src emacs-lisp :tangle no
(setq +zen-text-scale 0.8)
(defvar +zen-serif-p t
  "Whether to use a serifed font with `mixed-pitch-mode'.")
(defvar +zen-org-starhide t
  "The value `org-modern-hide-stars' is set to.")

(after! writeroom-mode
  (defvar-local +zen--original-org-indent-mode-p nil)
  (defvar-local +zen--original-mixed-pitch-mode-p nil)
  (defun +zen-enable-mixed-pitch-mode-h ()
    "Enable `mixed-pitch-mode' when in `+zen-mixed-pitch-modes'."
    (when (apply #'derived-mode-p +zen-mixed-pitch-modes)
      (if writeroom-mode
          (progn
            (setq +zen--original-mixed-pitch-mode-p mixed-pitch-mode)
            (funcall (if +zen-serif-p #'mixed-pitch-serif-mode #'mixed-pitch-mode) 1))
        (funcall #'mixed-pitch-mode (if +zen--original-mixed-pitch-mode-p 1 -1)))))
  (defun +zen-prose-org-h ()
    "Reformat the current Org buffer appearance for prose."
    (when (eq major-mode 'org-mode)
      (setq display-line-numbers nil
            visual-fill-column-width 80
            org-adapt-indentation nil)
      (when (featurep 'org-modern)
        (setq-local org-modern-star '("‚öú" "‚öú" "‚öú" "‚öú")
                    org-modern-hide-stars +zen-org-starhide)
        (org-modern-mode -1)
        (org-modern-mode 1))
      (setq
       +zen--original-org-indent-mode-p org-indent-mode)
      (org-indent-mode -1)))
  (defun +zen-nonprose-org-h ()
    "Reverse the effect of `+zen-prose-org'."
    (when (eq major-mode 'org-mode)
      (when (bound-and-true-p org-modern-mode)
        (org-modern-mode -1)
        (org-modern-mode 1))
      (when +zen--original-org-indent-mode-p (org-indent-mode 1))))
  (pushnew! writeroom--local-variables
            'display-line-numbers
            'visual-fill-column-width
            'org-adapt-indentation
            'org-modern-mode
            'org-modern-star
            'org-modern-hide-stars)
  (add-hook 'writeroom-mode-enable-hook #'+zen-prose-org-h)
  (add-hook 'writeroom-mode-disable-hook #'+zen-nonprose-org-h))
#+end_src

** journal
#+begin_src emacs-lisp :tangle no
(use-package! org-journal
    :config
    (setq org-journal-file-type 'weekly
          org-journal-enable-agenda-integration t
          org-journal-date-format "%Y-%m-%d, %A")
    )
#+end_src
* Mu4e
Mu4e is an email client for Emacs. This configuration sets up multiple email accounts and integrates various functionalities.

#+begin_src emacs-lisp :tangle no
;; Add mu4e to the load path
(add-to-list 'load-path "/usr/share/emacs/site-lisp/mu4e/")

;; Toggle org-msg in mu4e
(setq +mu4e-compose-org-msg-toggle-next nil)

;; Setting msmtp for sending emails
(after! mu4e
  (setq sendmail-program (executable-find "msmtp")
        send-mail-function #'smtpmail-send-it
        message-sendmail-f-is-evil t
        message-sendmail-extra-arguments '("--read-envelope-from")
        message-send-mail-function #'message-send-mail-with-sendmail))

;; Configure mu4e contexts for different email accounts
(set-email-account! "iitk"
                    '((mu4e-sent-folder             . "/iitk/Sent")
                      (mu4e-drafts-folder           . "/iitk/Drafts")
                      (mu4e-trash-folder            . "/iitk/Trash")
                      (mu4e-refile-folder           . "/iitk/All Mail")
                      (user-mail-address            . "viveksk21@iitk.ac.in")
                      (user-full-name               . "Lumi Dantu")
                      (smtpmail-smtp-user           . "viveksk21@iitk.ac.in")
                      (smtpmail-default-smtp-server . "mmtp.iitk.ac.in")
                      (smtpmail-smtp-server         . "smtp.cc.iitk.ac.in")
                      (smtpmail-smtp-service        .  465)
                      (mu4e-compose-signature       . "Krishna Dantu,\n210299"))
                    t)

(set-email-account! "gmail"
                    '((mu4e-sent-folder       . "/gmail/[Gmail]/Sent Mail")
                      (mu4e-drafts-folder     . "/gmail/[Gmail]/Drafts")
                      (mu4e-trash-folder      . "/gmail/[Gmail]/Bin")
                      (mu4e-refile-folder     . "/gmail/[Gmail]/All Mail")
                      (user-mail-address      . "lumidenoir@gmail.com")
                      (user-full-name         . "lumi denoir")
                      (smtpmail-smtp-user     . "lumidenoir@gmail.com")
                      (smtpmail-smtp-server   . "smtp.gmail.com")
                      (smtpmail-smtp-service  .  465)
                      (mu4e-compose-signature . "Yours truly,\nLumi Denoir"))
                    t)

;; Prompt for context if not specified
(setq mu4e-context-policy 'ask-if-none
      mu4e-update-interval 300
      mu4e-compose-context-policy 'always-ask
      mu4e-index-cleanup nil
      mu4e-index-lazy-check t)
#+end_src

* Latex mode
Configure LaTeX mode to use Zathura as the default viewer and set up cdlatex keybindings.

#+begin_src emacs-lisp :tangle yes
(setq +latex-viewers '(zathura))
(map! :map cdlatex-mode-map
      :i "TAB" #'cdlatex-tab) ;; Use TAB for cdlatex completion
#+end_src

* nov.el
nov.el is an Emacs mode for reading EPUB files. This configuration customizes the appearance and behavior of nov-mode.

#+begin_src emacs-lisp :tangle no
(use-package! calibredb
  :commands calibredb
  :config
  (setq calibredb-root-dir "~/Calibre Library"
        calibredb-db-dir (expand-file-name "metadata.db" calibredb-root-dir)))
#+end_src

#+begin_src emacs-lisp :tangle yes
(use-package! nov
  :mode ("\\.epub\\'" . nov-mode)
  :config
  (map! :map nov-mode-map
        :n "RET" #'nov-scroll-up)

  (advice-add 'nov-render-title :override #'ignore)

  (defun +nov-mode-setup ()
    "Tweak nov-mode to our liking."
    (face-remap-add-relative 'variable-pitch
                             :family "Alegreya"
                             :height 1.1
                             :width 'semi-expanded)
    (face-remap-add-relative 'default :height 1.1)
    (variable-pitch-mode 1)
    (setq-local line-spacing 0.2
                next-screen-context-lines 4
                shr-use-colors nil)
    (when (require 'visual-fill-column nil t)
      (setq-local visual-fill-column-center-text t
                  visual-fill-column-width 64
                  nov-text-width 106)
      (visual-fill-column-mode 1))
    (when (featurep 'hl-line-mode)
      (hl-line-mode -1))
    ;; Re-render with new display settings
    (nov-render-document)
    ;; Look up words with the dictionary.
    (add-to-list '+lookup-definition-functions #'+lookup/dictionary-definition))

  (add-hook 'nov-mode-hook #'+nov-mode-setup))

(defun org-nov-open-new-window (path)
  "Open nov.el link in a new window."
  (setq available-windows
        (delete (selected-window) (window-list)))
  (setq new-window
        (or (car available-windows)
            (split-window-sensibly)
            (split-window-right)))
  (select-window new-window)
  (nov-org-link-follow path))
#+end_src

* Completion
#+begin_src emacs-lisp :tangle yes
(use-package! corfu
  :config
  (defun corfu-enable-in-minibuffer ()
    "Enable Corfu in the minibuffer if `completion-at-point' is bound."
    (when (where-is-internal #'completion-at-point (list (current-local-map)))
      ;; (setq-local corfu-auto nil) ;; Enable/disable auto completion
      (setq-local corfu-echo-delay nil ;; Disable automatic echo and popup
                  corfu-popupinfo-delay nil)
      (corfu-mode 1)))
  (add-hook 'minibuffer-setup-hook #'corfu-enable-in-minibuffer))
(use-package! orderless
  :config
  (add-to-list 'orderless-matching-styles 'char-fold-to-regexp))

(setq corfu-auto-delay 0.3)
(setq yas-triggers-in-field t)

(custom-set-faces! '((corfu-popupinfo) :height 0.9))
#+end_src

* Tools
** IPYNB to markdown

#+begin_src emacs-lisp :tangle no
(defun ipynb-to-markdown (file)
  (interactive "f")
  (let* ((data (with-temp-buffer
                 (insert-file-contents file)
                 (json-parse-buffer :object-type 'alist
                                    :array-type 'list)))
         (metadata (alist-get 'metadata data))
         (language-info (alist-get 'language_info metadata))
         (language (alist-get 'name language-info)))
    (pop-to-buffer "ipynb-as-markdown")
    (when (featurep 'markdown-mode)
      (markdown-mode))
    (dolist (cell (alist-get 'cells data))
      (let ((cell-type (alist-get 'cell_type cell))
            (source (alist-get 'source cell))
            (outputs (alist-get 'outputs cell)))
        (pcase cell-type
          ("markdown"
           (when source
             (mapc #'insert source)
             (insert "\n\n")))
          ("code"
           (when source
             (insert (format "```%s\n" language))
             (mapc #'insert source)
             (insert "\n```\n\n")
             (dolist (output outputs)
               (let ((output-text (alist-get 'text output))
                     (output-data (alist-get 'data output)))
                 (when output-text
                   (insert "```stdout\n")
                   (insert (mapconcat #'identity output-text ""))
                   (insert "\n```\n\n"))
                 (when output-data
                   (when-let ((image64 (alist-get 'image/png output-data)))
                     (let ((image-data (base64-decode-string image64)))
                       (insert-image (create-image image-data 'png t))
                       (insert "\n\n")))))))))))))

#+end_src
** Markdown to Org
#+begin_src emacs-lisp :tangle no

#+end_src

** Right align tags in org buffers :HACK:

#+begin_src emacs-lisp :tangle no
(add-to-list 'font-lock-extra-managed-props 'display)

(defun my-org-mode-font-lock-setup ()
  "Set up custom font lock for org-mode."
  (font-lock-add-keywords nil
                          `(("^.*?\\( \\)\\(:[[:alnum:]_@#%:]+:\\)$"
                             (1 `(face nil
                                  display (space :align-to (- right ,(org-string-width (match-string 2)) 3)))
                                prepend))) t))

(add-hook 'org-mode-hook 'my-org-mode-font-lock-setup)
#+end_src

** Spell checking

#+begin_src emacs-lisp :tangle no
(use-package! jinx
  :defer t
  :init
  (add-hook 'doom-init-ui-hook #'global-jinx-mode)
  :config
  ;; Use my custom dictionary
  ;; (setq jinx-languages "en_US")
  ;; Extra face(s) to ignore
  (push 'org-inline-src-block
        (alist-get 'org-mode jinx-exclude-faces))
  ;; Take over the relevant bindings.
  (after! ispell
    (global-set-key [remap ispell-word] #'jinx-correct))
  (after! evil-commands
    (global-set-key [remap evil-next-flyspell-error] #'jinx-next)
    (global-set-key [remap evil-prev-flyspell-error] #'jinx-previous))
  ;; I prefer for `point' to end up at the start of the word,
  ;; not just after the end.
  (advice-add 'jinx-next :after (lambda (_) (left-word))))

#+end_src

** Changelog automation

#+begin_src emacs-lisp :tangle no
(defun ldn/update-project-changelog ()
  "Update ChangeLog.org with latest git commits under Changelog heading."
  (interactive)
  (let ((readme-file (expand-file-name "ChangeLog.org" (projectile-project-root)))
        (commit-log (shell-command-to-string (expand-file-name "extract_commits.sh" (projectile-project-root)))))
    (if (string-match-p "No new commits to add." commit-log)
        (message "No new commits to add.")
      (with-current-buffer (find-file-noselect readme-file)
        (goto-char (point-min))
        ;; Find the Changelog heading
        (if (re-search-forward "^\\*+ Changelog" nil t)
            (progn
              (forward-line)
              (insert commit-log "\n"))
          (goto-char (point-max))
          (insert "\n* Changelog\n" commit-log "\n"))
        (save-buffer)
        (message "Changelog updated in ChangeLog.org!")))))

(map! :leader
      :desc "Update ChangeLog.org"
      "p u" #'ldn/update-project-changelog)

#+end_src

** symbol-prettifying

#+begin_src emacs-lisp :tangle yes
(defun soph/prettify-symbols-setup ()
  "Beautify keywords"
  (setq prettify-symbols-alist
        (mapcan (lambda (x) (list x (cons (upcase (car x)) (cdr x))))
                '(("#+name:" . "¬ª")
                  ("#+title:" . "")
                  ("#+author:" . "")
                  ("#+description:" . "")
                  ("#+email" . "ÔêØ")
                  ("#+date" . "Û∞¢ß")
                  ("#+property" . "Û∞†≥")
                  ("#+options" . #("Û∞òµ" 0 1 (display (height 0.75))))
                  ("#+startup" . "‚èª")
                  ("#+macro" . "‚Ñ≥")
                  ("#+bind" . "Û∞å∑")
                  ("#+bibliography" . "ÔêÖ")
                  ("#+print_bibliography" . "Û∞å±")
                  ("#+cite_export" . "ÔêÖ‚Æ≠")
                  ("#+filetags:" . "Û∞ìπ")
                  ("#+EXPORT_FILE_NAME" . "Óâº")
                ;;("print_glossary" . "Û∞å±·¥¨·∂ª")
                ;;("glossary_sources" . "Û∞íª")
                  ("#+include" . "‚á§")
                  ("#+setupfile" . "‚áö")
                  ("#+html_head" . "üÖ∑")
                  ("#+html" . "üÖó")
                  ("#+latex_class" . "üÑª")
                  ("#+latex_class_options" . "üÑªÛ∞íì")
                  ("#+latex_header" . "üÖª")
                  ("#+latex_header_extra" . "üÖª‚Å∫")
                  ("#+latex" . "üÖõ")
                  ("#+beamer_theme" . "üÑ±")
                  ("#+beamer_color_theme" . "üÑ±Û∞èò")
                  ("#+beamer_font_theme" . "üÑ±ùêÄ")
                  ("#+beamer_header" . "üÖ±")
                  ("#+beamer" . "üÖë")
                  ("#+attr_latex" . "üÑõ")
                  ("#+attr_html" . "üÑó")
                  ("#+attr_org" . "‚í™")
                  ("#+call" . "Û∞úé")
                  ("#+header" . "‚Ä∫")
                  ("#+caption" . "‚ò∞")
                  ("#+results" . "Óà¥")
                  ("[ ]" . "ÓôÄ")
                  ("[X]" . "Û∞Ñµ")
                  ("[-]" . "Û∞°ñ")
                  ("#+begin_src" . "Û∞Ñæ")
                  ("#+end_src" . "Û∞Ñæ")
                  ("#+begin_quote" . "Û∞ùó")
                  ("#+end_quote" . "Û∞âæ")
                  ("#+begin_verse" . "Û∞¥ì")
                  ("#+end_verse" . "Û∞¥ì")
                  ("#+begin_example" . "ÔÅö")
                  ("#+end_example" . "ÔÅö")
                  (":PROPERTIES:" . "Óûô")
                  ("SCHEDULED:" . "Û±°°")
                  ("DEADLINE:" . "Û∞•ï")
                  ("CLOSED:" . "Û∞æ®"))))
  (prettify-symbols-mode))

(add-hook 'org-mode-hook        #'soph/prettify-symbols-setup)
(add-hook 'org-agenda-mode-hook #'soph/prettify-symbols-setup)

;; Function to print the final value of prettify-symbols-alist for debugging
(defun print-prettify-symbols-alist ()
  "Print the prettify-symbols-alist for debugging."
  (interactive)
  (message "prettify-symbols-alist: %s" prettify-symbols-alist))

;; Add a hook to print the value after setup
(add-hook 'org-mode-hook #'print-prettify-symbols-alist)
(add-hook 'org-agenda-mode-hook #'print-prettify-symbols-alist)
#+end_src

** gptel
#+begin_src emacs-lisp :tangle no
(setq
 gptel-model "qwen3:0.6b"
 gptel-backend (gptel-make-ollama "Ollama"
                 :host "localhost:11434"
                 :stream t
                 :models '("qwen3:0.6b")))
#+end_src
** Hippie expand
#+begin_src emacs-lisp :tangle yes
(global-set-key [remap dabbrev-expand] #'hippie-expand)

(setq hippie-expand-try-functions-list
      '(try-expand-list
        try-expand-dabbrev-visible
        try-expand-dabbrev
        try-expand-all-abbrevs
        try-expand-dabbrev-all-buffers
        try-complete-file-name-partially
        try-complete-file-name
        try-expand-dabbrev-from-kill
        try-expand-whole-kill
        try-expand-line
        try-complete-lisp-symbol-partially
        try-complete-lisp-symbol))
#+end_src
** Ruff format
#+begin_src emacs-lisp :tangle yes
(after! apheleia
  (setf (alist-get 'python-mode apheleia-mode-alist)
        '(ruff-isort ruff))
  (setf (alist-get 'python-ts-mode apheleia-mode-alist)
      '(ruff-isort ruff)))
#+end_src
* Flutter & Dart
#+begin_src emacs-lisp :tangle yes
(setq lsp-dart-sdk-dir "~/flutter/bin/cache/dart-sdk"
      lsp-dart-flutter-sdk "~/flutter"
      flutter-sdk-path "~/flutter")
#+end_src
